import org.springframework.boot.gradle.run.BootRunTask

buildscript {
    ext {
        springBootVersion = '1.5.9.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

plugins {
    id 'com.gradle.build-scan' version '1.11'
}

apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'

jar {
    baseName = 'payments'
    version = '0.0.1-SNAPSHOT'
    archiveName = "${baseName}.${extension}"
}

springBoot {
    executable = true
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    maven {
        url "https://github.com/stil4m/maven-repository/raw/master/releases/"
    }
    maven {
        url 'https://dl.bintray.com/wisvch/maven'
    }
}

configurations {
    // We're using HikariCP instead
    compile.exclude module: "tomcat-jdbc"
    // We're using Log4j2 instead
    compile.exclude module: "spring-boot-starter-logging"
}

dependencies {
    compile('org.springframework.boot:spring-boot-starter-data-jpa')
    compile('org.springframework.boot:spring-boot-devtools')
    compile('org.springframework.boot:spring-boot-starter-security')
    compile('org.springframework.boot:spring-boot-starter-web')
    compile("org.springframework.boot:spring-boot-starter-log4j2")
    compile("org.springframework.boot:spring-boot-starter-mail")
    compile("org.springframework.boot:spring-boot-starter-actuator")

    compile('org.projectlombok:lombok')
    compile("org.hibernate:hibernate-validator")
    compile("com.fasterxml.jackson.datatype:jackson-datatype-jsr310")
    compile('com.jayway.jsonpath:json-path:2.4.0')
    compile("com.zaxxer:HikariCP")
    compile("org.flywaydb:flyway-core:4.0.1")
    compile("org.webjars:bootstrap:3.3.7-1")
    compile("org.webjars:font-awesome:4.7.0")
    compile("org.webjars.bower:wisvch-bootstrap-theme:0.0.3")

    compile("org.thymeleaf:thymeleaf-spring4:3.0.6.RELEASE")
    compile("org.thymeleaf:thymeleaf:3.0.6.RELEASE")
    compile("org.thymeleaf.extras:thymeleaf-extras-springsecurity4:3.0.2.RELEASE")

    compile("nl.stil4m:mollie-api:2.6.2")

    compile("ch.wisv:wisvch-connect-client:1.3.1.7")

    compileOnly('org.springframework.boot:spring-boot-configuration-processor')

    runtime('org.postgresql:postgresql')
    runtime("org.hsqldb:hsqldb")

    testCompile 'io.rest-assured:rest-assured:3.0.3'
    testCompile('org.springframework.boot:spring-boot-starter-test')
}

task dev(type: BootRunTask) {
    group = 'Application'
    doFirst() {
        main = project.mainClassName
        classpath = sourceSets.main.runtimeClasspath
        systemProperty 'spring.profiles.active', 'dev'
    }
}

buildScan {
    licenseAgreementUrl = 'https://gradle.com/terms-of-service'
    licenseAgree = 'yes'
}
